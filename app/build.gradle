apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    def VERSION_CODE = 15
    def VERSION_NAME = "0.2.5"
    def APP_NAME = "dandelion*"
    def APP_PACKAGE = "com.github.dfa.diaspora_android"
    def APP_ICON = "@drawable/ic_launcher"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 24

        buildConfigField "boolean", "IS_TEST_BUILD", "false"
        buildConfigField "boolean", "IS_GPLAY_BUILD", "false"

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        flavorDefault {
            applicationId APP_PACKAGE
            resValue 'string', 'app_name', APP_NAME
            manifestPlaceholders = [appIcon: APP_ICON]
            versionCode VERSION_CODE; versionName VERSION_NAME
        }

        flavorTest {
            applicationId APP_PACKAGE.replace("diaspora_android", "secondlion")
            resValue 'string', 'app_name', "secondlion*"
            manifestPlaceholders = [appIcon: APP_ICON + "_test"]
            versionCode = Integer.parseInt(new Date().format('yyMMdd'))
            versionName = new Date().format('yyMMdd')
            buildConfigField "boolean", "IS_TEST_BUILD", "true"
        }

 /*       flavorGplay {
            applicationId APP_PACKAGE
            resValue 'string', 'app_name', APP_NAME
            manifestPlaceholders = [appIcon: APP_ICON]
            versionCode VERSION_CODE; versionName VERSION_NAME
            buildConfigField "boolean", "IS_GPLAY_BUILD", "true"
        }
*/
    }
    lintOptions {
        disable 'MissingTranslation'
    }
}

// Additional repositories
repositories {
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    // Sub-Projects
    //compile project(':subprojectFromRoot')

    // Jars
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    // Android standard libs
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.1.0' //Don't update. Broken up to 25.0.0
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.android.support:customtabs:24.2.1'
    compile 'com.android.support:cardview-v7:24.2.1'

    // More libraries
    compile 'com.jakewharton:butterknife:8.0.1'
    compile 'info.guardianproject.netcipher:netcipher:2.0.0-alpha1'
    compile 'info.guardianproject.netcipher:netcipher-webkit:2.0.0-alpha1'
    compile 'com.github.DASAR:ShiftColorPicker:v0.5'
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.6.0'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
}

//  #####################
//  Groovy Coding Area
//  #####################
final String RAW_DIR = "app/src/main/res/raw"
final String[] ROOT_TO_RAW_COPYFILES = ["README.md", "LICENSE.md", "CHANGELOG.md", "CONTRIBUTORS.txt"]

// Called before building
task copyRepoFiles(type: Copy) {

    // Copy files over to raw dir
    from rootProject.files(ROOT_TO_RAW_COPYFILES)
    into RAW_DIR.replaceFirst("app/", "")
    rename { String fileName ->
        fileName.replace(fileName, fileName.toLowerCase())
    }

    // Filter Contributors file
    from(rootProject.file("CONTRIBUTORS.txt")) {
        into '.'    // Target already changed to 'src/main/res/raw'
        rename { String fileName ->
            fileName.replace(fileName, fileName.toLowerCase())
        }
        filter { line ->
            (line.toString().matches("..l>>.*") || line.toString().startsWith("## 99l CONTRIBUTORS")) ? null : line.toString().trim().replaceAll(" \\(.*\\)", "")
        }
    }
}
tasks.copyRepoFiles.execute()
